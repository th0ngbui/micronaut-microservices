buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }
    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:5.0.0"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.5.RELEASE"
    }
}

subprojects { project ->

    apply plugin: "io.spring.dependency-management"

    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        jcenter()
    }

    println ">>>" + project.path
    if (project.path.contains('-host')) {

        apply plugin: "java"
        apply plugin: "application"
        apply plugin: 'com.github.johnrengelman.shadow'
        shadowJar {
            mergeServiceFiles()
        }

        dependencyManagement {
            imports {
                mavenBom "io.micronaut:micronaut-bom:$micronautVersion"
            }
        }

        dependencies {
            annotationProcessor "io.micronaut:micronaut-inject-java"
            annotationProcessor "io.micronaut.configuration:micronaut-openapi"
            annotationProcessor("org.projectlombok:lombok:${lombokVersion}")

            compileOnly "io.micronaut:micronaut-inject-java"
            compileOnly "org.projectlombok:lombok:${lombokVersion}"

            compile "io.micronaut.configuration:micronaut-openapi"
            compile "io.micronaut.configuration:micronaut-hibernate-validator"
            compile "io.micronaut:micronaut-http-server-netty"
            compile "io.micronaut:micronaut-discovery-client"
            compile "io.micronaut:micronaut-runtime"

            runtime "io.micronaut:micronaut-management"
            runtime "ch.qos.logback:logback-classic:1.2.3"

            testCompile "junit:junit:4.12"
            testCompile "io.micronaut:micronaut-inject-java"
            testCompile "io.micronaut:micronaut-http-client"
        }
    }

    if (project.path.contains('-api')) {
        apply plugin: "java"
        dependencies {
            annotationProcessor "org.projectlombok:lombok:$lombokVersion"
            compileOnly "org.projectlombok:lombok:${lombokVersion}"
            compile 'io.reactivex.rxjava2:rxjava:2.2.2'
        }
    }

    if (project.path.contains('-domain')) {
        apply plugin: "java"
        dependencies {
            annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
            compileOnly "org.projectlombok:lombok:${lombokVersion}"

            compile 'io.reactivex.rxjava2:rxjava:2.2.2'
            compile 'org.apache.commons:commons-lang3:3.8.1'
            compile 'net.sf.dozer:dozer:5.5.1'
        }
    }
}

wrapper {
    gradleVersion = '5.0'
}

task clean(type: Delete) {
    delete rootProject.buildDir
}